group 'com.ironz'
version '0.0.3'

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {

    compileSdkVersion 24
    buildToolsVersion '24.0.0'

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    defaultConfig {
        minSdkVersion 14
        maxSdkVersion 24
        versionCode getGitActualVersionCode()
        versionName getGitActualTagName()
    }

    libraryVariants.all { variant ->
        variant.outputs.each { output ->
            output.outputFile = new File(
                    output.outputFile.parent,
                    output.outputFile.name.replace(".aar", getVersionName()))
        }
    }
}

def getVersionName() {
    "-${getGitActualVersionCode()}-${getGitActualTagName()}-${getGitRevParseInfo("--short")}.aar"
}

def getGitActualVersionCode() {
    def cmd = "git tag"
    def proc = cmd.execute()
    proc.text.split("\r\n").length + 1
}

def getGitActualTagName() {
    def cmd = "git describe --abbrev=0 --tags"
    def proc = cmd.execute()
    proc.text.trim()
}

def getGitRevParseInfo(what) {
    def cmd = "git rev-parse ${what} HEAD"
    def proc = cmd.execute()
    proc.text.trim()
}

buildscript {

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:2.1.2'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.0.3'
    }
}

repositories {
    mavenCentral()
    jcenter()
}

dependencies {

    testCompile 'junit:junit:4.11'
    testCompile "org.robolectric:robolectric:3.1"
    testCompile "org.assertj:assertj-core:3.4.1"

    testCompile 'com.android.support:support-annotations:24.0.0'
    testCompile 'com.android.support:appcompat-v7:24.0.0'
    testCompile 'org.jetbrains.kotlin:kotlin-stdlib:1.0.3'

    compile 'com.android.support:support-annotations:24.0.0'
    compile 'org.jetbrains.kotlin:kotlin-stdlib:1.0.3'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.13'
}
